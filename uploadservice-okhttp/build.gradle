apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

// start - module specific overrides of default values written in manifest.gradle
def bintray_project_name = "android-upload-service-okhttp"
def library_description = "OkHttp stack implementation for Android Upload Service."
def library_keywords = ['android', 'upload', 'service', 'okhttp', 'stack']
// end - module specific overrides

// start - do not modify this if your project is on github
project.ext{
    mavDevelopers = [(properties.getProperty("bintray.user")):(maintainer)]
    mavSiteUrl = "https://github.com/${github_username}/${github_repository_name}"
    mavGitUrl = mavSiteUrl + '.git'
    bugTrackerUrl = mavSiteUrl + '/issues/'
    mavProjectName = bintray_project_name
    mavLibraryLicenses = ["Apache-2.0": 'http://www.apache.org/licenses/LICENSE-2.0.txt']
    mavLibraryDescription = library_description
    mavVersion = library_version
}
// end - do not modify this if your project is on github

group = library_project_group
version = library_version

android {
    compileSdkVersion target_sdk

    defaultConfig {
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode version_code
        versionName library_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    // Testing - https://developer.android.com/training/testing/set-up-project
    testImplementation "junit:junit:$junit_version"

    // Core library
    androidTestImplementation "androidx.test:core:$androidx_test_core_version"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_rules_version"

    // Assertions
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_ext_junit_version"
    androidTestImplementation "androidx.test.ext:truth:$androidx_test_ext_truth_version"
    androidTestImplementation "com.google.truth:truth:$truth_version"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_test_espresso_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'com.squareup.okhttp3:okhttp:4.0.1'
    //api "net.gotev:uploadservice:${version}"
    //comment the previous line and uncomment the next line for development (it uses the local lib)
    api project(':uploadservice')
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenPublish']
    pkg {
        repo = "maven"
        name = mavProjectName
        desc = library_description
        websiteUrl = mavSiteUrl
        vcsUrl = mavGitUrl
        issueTrackerUrl = bugTrackerUrl
        licenses = library_licenses
        labels = library_keywords
        publicDownloadNumbers = true
        publish = true
    }
}

apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'
